apply plugin: 'com.android.application'


/**
 * 开发
 */
def debugUrl = "http://agent.test.ysbjob.com/"
/**
 * 生产  -- 绝对不可更改
 */
def releaseUrl = "http://agent.ysbjob.com/"

//APK前缀
def apkPrefix() { return "ysb" }
//APK后缀
def apkSuffix() { return "apk" }

def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        ndk {
            abiFilters "armeabi-v7a" //设置支持的SO库架构（开发者可以根据需要，选择一个或多个平台的so）
        }

        javaCompileOptions {
            annotationProcessorOptions {
                // 更新 build.gradle, 添加参数 AROUTER_GENERATE_DOC = enable
                // 生成的文档路径 : build/generated/source/apt/(debug or release)/com/alibaba/android/arouter/docs/arouter-map-of-${moduleName}.json
                arguments = [AROUTER_MODULE_NAME: project.getName(), AROUTER_GENERATE_DOC: "enable"]
            }
        }
    }

    /**
     * 资源设置
     */
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    /**
     * 签名扩展信息
     */
    ext.myStoreFile = file("./keystores/ysb_employer_keystore.jks")
    ext.myStorePassword = 'ysb123456'
    ext.myKeyAlias = 'yushibao'
    ext.myKeyPassword = 'ysb123456'
    /**
     * 签名
     */
    signingConfigs {
        debug {
            storeFile myStoreFile
            storePassword myStorePassword
            keyAlias myKeyAlias
            keyPassword myKeyPassword
        }

        release {
            storeFile myStoreFile
            storePassword myStorePassword
            keyAlias myKeyAlias
            keyPassword myKeyPassword
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    buildTypes {
        debug {
            multiDexEnabled true
            minifyEnabled false //最小化资源包
            shrinkResources false//去掉无用资源
            signingConfig signingConfigs.debug
            buildConfigField "String", "SERVER_URL", "\"" + debugUrl + "\""
        }
        // 生产（发布环境）
        release {
            multiDexEnabled true
            minifyEnabled true
            shrinkResources true//去掉无用资源
            signingConfig signingConfigs.release
            buildConfigField "String", "SERVER_URL", "\"" + releaseUrl + "\""
        }
    }

    /**
     * 编译APK名称
     */
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${apkPrefix()}_${defaultConfig.versionName}_${variant.buildType.name}_${releaseTime()}.${apkSuffix()}"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    annotationProcessor annotationLibs

    api dependenciesExt.appDependencies
    api dependenciesExt.frameworkDependencies
    api dependenciesExt.thirdPartyDependencies
    api dependenciesExt.networkDependencies
    api dependenciesExt.utilsDependencies
    api dependenciesExt.widgetDependencies
}
